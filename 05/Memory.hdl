// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    // RAM is memory-mapped at      0b0000_0000_0000_0000 ~ 0b0111_1111_1111_1111 (0 ~ 16383)
    // Screen is memory-mapped at   0b1000_0000_0000_0000 ~ 0b1011_1111_1111_1111 (16384 ~ 24575)
    // Keyboard is memory-mapped at 0b1100_0000_0000_0000 ~ 0b1100_0000_0000_0000 (24576 ~ 24576)
    // So, if address[14] == 0, then the access is RAM
    // else if address[13] == 0, then the access is Screen
    // else, keyboard
    
    // First, dmux load. if address[14] is 0, ram. else, screen
    DMux(in=load, sel=address[14], a=ram, b=scr);
    // call all devices
    RAM16K(in=in, load=ram, address=address[0..13], out=ramout);
    Screen(in=in, load=scr, address=address[0..12], out=screenout);
    Keyboard(out=kbdout);
    // Choose which output should be the result
    // if address[13] is 0, the use screen output. else, use keyboard output.
    Mux16(a=screenout, b=kbdout, sel=address[13], out=scrOrKey);
    // Finally, choose output by address[14].
    Mux16(a=ramout, b=scrOrKey, sel=address[14], out=out);
}
